// --------------------------------------------
// Mod setup
// --------------------------------------------
// Basics
BACKUP ~sarevokfriendship/backup~
AUTHOR ~Aeryn~
VERSION ~v2.6~
README ~sarevokfriendship/sarevokfriendship-readme-%LANGUAGE%.html~ ~sarevokfriendship/sarevokfriendship-readme-english.html~

// Translations
AUTO_TRA ~sarevokfriendship/translations/%s~

ALWAYS

	/* eet compatibility */
	ACTION_IF (GAME_IS ~eet~) BEGIN
		OUTER_SET bg2_chapter = 12
	END ELSE BEGIN
		OUTER_SET bg2_chapter = 0
	END
	OUTER_FOR (i=1; i<=10; i=i+1) BEGIN
		OUTER_SET bg2_chapter = bg2_chapter + 1
		OUTER_SPRINT name_source ~bg2_chapter_%i%~
		OUTER_SET EVAL ~%name_source%~ = bg2_chapter
	END


	/* Converts tra files to UTF-8 */
	// No exception, everything needs to be converted
	ACTION_DEFINE_ARRAY SarFrienoconvert BEGIN END

	// any tra files used in LANGUAGE need to be reloaded after UTF-8 conversion
	ACTION_DEFINE_ARRAY SarFriereload BEGIN setup END

	LAF HANDLE_CHARSETS
		INT_VAR
			infer_charsets = 1
		STR_VAR
			tra_path = EVAL ~%MOD_FOLDER%/translations~
			noconvert_array = SarFrienoconvert
			reload_array = SarFriereload
	END

END

LANGUAGE ~English~
         ~english~
         ~sarevokfriendship/translations/english/setup.tra~

LANGUAGE ~Francais (traduction : Isaya)~
         ~french~                                       
         ~sarevokfriendship/translations/french/setup.tra~

LANGUAGE ~Polish (translation by Astharoshe, member of SoM)~
         ~polish~
         ~sarevokfriendship/translations/polish/setup.tra~

LANGUAGE ~Chinese (translation by Merzbau)~
         ~schinese~
         ~sarevokfriendship/translations/schinese/setup.tra~

LANGUAGE ~Russian (translation by Arkie)~
		 ~russian~
		 ~sarevokfriendship/translations/russian/setup.tra~

/* ===================================== *
 *    Sarevok Friendship for BG2: ToB    *
 * ===================================== */

BEGIN @0
REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @1 /* ToB is not installed! Please install the Throne of Bhaal expansion before installing the Sarevok Friendship. */
DESIGNATED 0 LABEL ~sarevok_friendship~

/* CamDawg's custom IsValidForPartyDialogue() state. Thanks Cam!
   covers STATE_CONFUSED, STATE_FEEBLEMINDED, STATE_SILENCED, STATE_DEAD, STATE_ACID_DEATH, STATE_FLAME_DEATH, STATE_EXPLODING_DEATH, STATE_STONE_DEATH, STATE_FROZEN_DEATH, STATE_HELPLESS, STATE_STUNNED, STATE_PANIC, STATE_BERSERK, STATE_SLEEPING */
APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~CD_STATE_NOTVALID~


// Compiling dialogue & scripts
COMPILE ~sarevokfriendship/dialogue/sarev25j.d~
EXTEND_BOTTOM ~sarev25.bcs~ ~sarevokfriendship/scripts/sarev25.baf~
